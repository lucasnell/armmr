model{
#===============
#Priors
#===============

#Means
mean_b0 ~ dnorm(0, 0.01)
mean_b1 ~ dnorm(0, 0.01)
mean_rho ~ dnorm(0, 0.25)

#Standard Deviations
sigma_b0 ~ dnorm(0, 0.01)T(0,)
sigma_b1 ~ dnorm(0, 0.01)T(0,)
sigma_rho ~ dnorm(0, 0.5)T(0,)
sigma_eps ~ dnorm(0, 0.01)T(0,)
sigma_obs ~ dnorm(0, 0.01)T(0,)

#Taus
tau_b0 = pow(sigma_b0, -2)
tau_b1 = pow(sigma_b1, -2)
tau_rho = pow(sigma_rho, -2)
tau_e = pow(sigma_eps, -2)
tau_obs = pow(sigma_obs, -2)



#===============
#Variation by Species
#===============

for(s in 1:n_spp){

#Intercepts and slopes
b0[s] ~ dnorm(mean_b0, tau_b0)
b1[s] ~ dnorm(mean_b1, tau_b1)

#AR1 parameter
rho_logit[s] ~ dnorm(mean_rho, tau_rho)
rho[s] = exp(rho_logit[s])/(1 + exp(rho_logit[s]))

} #s



#===============
#Likelihood
#===============

#Loop over sites, species, and time

for(l in 1:n_loc){

  for(s in 1:n_spp){

#Initial population size
n[l,s,1] ~ dnorm(n_init[l,s], 0.5*abs(n_init[l,s]))
  
    for(t in 2:n_time){
    
#Environmental Stochasticity
e[l,s,t] ~ dnorm(0, tau_e)

#State process    
n[l,s,t] = b0[s] + b1[s]*x[l,t] + rho[s]*(n[l,s,t-1] - b0[s] - b1[s]*x[l,t-1]) + e[l,s,t]

    } #t
    
    for(t in 1:n_time){
    
#Observation process
n_obs[l,s,t] ~ dnorm(n[l,s,t], tau_obs)

    } #t
  } #s
} #l

}


